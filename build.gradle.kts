import java.nio.file.Path
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id("java-library")
	id("maven-publish")
	id("idea")
	id("net.neoforged.moddev") version "2.0.140"
	kotlin("jvm") version "2.2.20"
}

version = mod_version
group = mod_group_id


base {
	archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
	// Specify the version of NeoForge to use.
	version = project.neo_version
	
	parchment {
		mappingsVersion = project.parchment_mappings_version
		minecraftVersion = project.parchment_minecraft_version
	}
	
	// This line is optional. Access Transformers are automatically detected
	// accessTransformers.add("src/main/resources/META-INF/accesstransformer.cfg")
	
	// Default run configurations.
	// These can be tweaked, removed, or duplicated as needed.
	runs {
		create("client") {
			client()
			
			// Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
			systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
		}
		
		create("server") {
			server()
			programArgument("--nogui")
			systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
		}
		
		// This run config launches GameTestServer and runs all registered gametests, then exits.
		// By default, the server will crash when no gametests are provided.
		// The gametest system is also enabled by default for other run configs under the /test command.
		create("gameTestServer") {
			type = "gameTestServer"
			systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
		}
		
		create("data") {
			data()
			
			// example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
			// gameDirectory = project.file("run-data")
			
			// Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
			programArguments.addAll("--mod", mod_id,
			                        "--all",
			                        "--output", file("src/generated/resources/").absolutePath,
			                        "--existing", file("src/main/resources/").absolutePath
			)
		}
		
		// applies to all the run configs above
		configureEach {
			// Recommended logging data for a userdev environment
			// The markers can be added/remove as needed separated by commas.
			// "SCAN": For mods scan.
			// "REGISTRIES": For firing of registry events.
			// "REGISTRYDUMP": For getting the contents of all registries.
			systemProperty("forge.logging.markers", "REGISTRIES")
			systemProperty("mixin.debug.export", "true") // -Dmixin.debug.export=true
//			jvmArgument("-XX:+AllowEnhancedClassRedefinition")
			
			// Recommended logging level for the console
			// You can set various levels here.
			// Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
			logLevel = org.slf4j.event.Level.DEBUG
		}
	}
	
	mods {
		// define mod <-> source bindings
		// these are used to tell the game which sources are for which mod
		// mostly optional in a single mod project
		// but multi mod projects should define one per mod
		create(mod_id) {
			sourceSet(sourceSets.main.get())
		}
	}
}

// Include resources generated by data generators.
sourceSets.main.get().resources { srcDir("src/generated/resources") }

repositories {
	maven(url="https://cursemaven.com")
	maven(url="https://jitpack.io")
	maven {
		name = "Kotlin for Forge"
		url = uri("https://thedarkcolour.github.io/KotlinForForge/")
	}
	maven(url="https://maven.bawnorton.com/releases")
	maven(url="https://oss.sonatype.org/content/repositories/snapshots/")
	mavenCentral()
}

dependencies {
	
	implementation("thedarkcolour:kotlinforforge-neoforge:5.11.0")
	compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:0.3.7-beta.1")!!)
	implementation("com.github.bawnorton.mixinsquared:mixinsquared-neoforge:0.3.7-beta.1")
	"jarJar"("com.github.bawnorton.mixinsquared:mixinsquared-neoforge:0.3.7-beta.1") {
		version {
			strictly("[0.3.7-beta.1,)")
		}
	}
	
	val localMods = Path.of(EPOCH_MODS_DIR)
	compileOnly(files(localMods.resolve("TheCatamount-neoforge-1.0.jar")))
	compileOnly(files(localMods.resolve("shineals_prehistoric_expansion-1.4.3-neoforge-1.21.1.jar")))
	implementation(files(localMods.resolve("architectury-13.0.8-neoforge.jar"), localMods.resolve("OctoLib-NEOFORGE-0.6.0.4+1.21.jar")))
	implementation(files(localMods.resolve("Clavis-NEOFORGE-0.2.11+1.21.1.jar")))
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
tasks.register<ProcessResources>("generateModMetadata") {
	val replaceProperties = mapOf("minecraft_version"      to minecraft_version,
							 "minecraft_version_range" to minecraft_version_range,
							 "neo_version"            to neo_version,
							 "neo_version_range"      to neo_version_range,
							 "loader_version_range"   to loader_version_range,
							 "mod_id"                 to mod_id,
							 "mod_name"               to mod_name,
							 "mod_license"            to mod_license,
							 "mod_version"            to mod_version,
							 "mod_authors"            to mod_authors,
							 "mod_description"        to mod_description)
	inputs.properties(replaceProperties)
	expand(replaceProperties)
	from("src/main/templates")
	into("build/generated/sources/modMetadata")
}.also {
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
	sourceSets.main.get().resources.srcDir(it)
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
	neoForge.ideSyncTask(it)
}



// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
	module {
		isDownloadSources = false
		isDownloadJavadoc = true
	}
}
val compileKotlin: KotlinCompile by tasks
compileKotlin.compilerOptions {
	freeCompilerArgs.set(listOf("-Xcontext-parameters"))
}